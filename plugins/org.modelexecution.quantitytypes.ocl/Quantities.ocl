-- %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
--
-- This file provides the OCL specification of the operations of types
-- "UReal", "Unit" and "Quantity", as defined in the paper
-- "Adding Tolerance and Units to Quantity Types in Software Models" 
-- by T. Mayerhofer, M. Wimmer and A. Vallecillo. In Proc. of SLE 2016.
-- The only difference is that class SIUnit has been incorporated into
-- class Unit, flattening the inheritance hierarchy between the two.
--
-- The operations in this file have been specified using standard OCL,
-- and they are not executable. A separate file "Quantities.use" contains
-- the same specifications in SOIL (Simple OCL-like Imperative Language) 
-- which is part of the USE environment and toolkit. For more info see:
-- http://www.db.informatik.uni-bremen.de/publications/Buettner_2014_SCPSOIL.pdf
-- The benefits of this approach is that SOIL specification can be executed.
--
-- These specifications assume operations "sqrt" and "power" on OCL type Real, 
-- currently not supported because they are not included in the OCL standard.
-- Similarly for the "sum", "minus" (subtraction of arrays) and "mult" 
-- (multiplication by a scalar) operations on Sequence(Real). 
--
-- AUTHOR:  A. Vallecillo, Tanja Mayerhofer and Manuel Wimmer. 
-- CONTACT: av@lcc.uma.es
-- VERSION: 1.0
-- DATE: 8 Sep 2016
-- 
-- Thanks to Martin Gogolla for his help, comments and suggestions on 
-- previous versions of this OCL/USE specification.

-- %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
-- assumes operations "sqrt" and "power" on OCL type Real.
-- context Real::sqrt():Real
-- context Real::power(exp:Real):Real
-- assumes operations on Sequence(Real)
-- context Sequence(Real)::sum(other:Sequence(Real)):Sequence(Real)
--   pre: self->size() = other->size()
--   post: Sequence{1..self.size}->forAll(result->at(i)=self->at(i)+other->at(i))
-- context Sequence(Real)::minus(other:Sequence(Real)):Sequence(Real)
--   pre: self->size() = other->size()
--   post: Sequence{1..self.size}->forAll(result->at(i)=self->at(i)-other->at(i))
-- context Sequence(Real)::mult(x:Real):Sequence(Real)
--   post: Sequence{1..self.size}->forAll(result->at(i)=x*self->at(i))
--
-- %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
-- Definiton of class "Unit" -- USE specific
-- 
-- class Unit
--   attributes
--     name : String
--     symbol : String
--     dimensions : Sequence(Real)
--     conversionFactor : Sequence(Real)
--     offset : Sequence(Real)
--   operations
--     isCompatibleWith(u :Unit) : Boolean
--     equals(u :Unit) :Boolean 
--     isBaseUnit() :Boolean
--     isDimensionless() :Boolean
--     isUnitless() :Boolean
--     factor() :Real -- reqired for conversions
--     isDerivedUnit() :Boolean =
--     multiplyUnits(x :Unit) :Unit  
--     divideUnits(x:Unit) :Unit
--     powerUnits(s:Real) :Unit
-- end
--
-- constraints

context Unit::isCompatibleWith(u :Unit) : Boolean
  post: result = (self.dimensions = u.dimensions)

context Unit::equals(u :Unit) :Boolean 
  post: result  = (self.dimensions = u.dimensions) and
    (self.conversionFactor = u.conversionFactor) and
    (self.offset = u.offset) 

context Unit::isBaseUnit() :Boolean
  post: result = (self.dimensions->count(1.0)=1) and 
    (self.dimensions->count(0.0)=7) and
    (self.noOffset()) and
    (self.conversionFactor->count(1.0)=8)

context Unit::isDimensionless() :Boolean
  post: result = (self.dimensions->count(0.0)=8)

context Unit::isUnitless() :Boolean
  post: result = (self.dimensions->subSequence(1,7)->count(0.0)=7)
  
context Unit::factor() :Real -- reqired for conversions
  post: result = Sequence{1..8}->
       iterate(i : Integer; acc : Real = 1.0 | 
          acc*(self.conversionFactor->at(i)).power(self.dimensions->at(i)))

context Unit::isDerivedUnit() :Boolean 
  post: result =
   let DerivedUnitDimensions  : Sequence(Sequence(Real)) = Sequence{	
            Sequence{0,0,0,0,0,0,0,0}, -- 'Gy',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Gy/s',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Bq',
            Sequence{0,0,0,0,0,0,0,0}, -- 'mol/m^3',
            Sequence{0,0,0,0,0,0,0,0}, -- 'mol/kg',
            Sequence{0,0,-2,0,0,0,0,0}, -- '1/s^2',
            Sequence{-2,1,0,0,0,0,0,0}, -- 'kg*m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J*s',
            Sequence{0,0,-1,0,0,0,0,0}, -- '1/s',
            Sequence{2,0,0,0,0,0,0,0}, -- 'm^2',
            Sequence{2,0,0,0,0,0,0,0}, -- 'm^2',
            Sequence{2,0,0,0,1,0,0,0}, -- 'm^2*K',
            Sequence{2,0,0,0,-1,0,0,0}, -- 'm^2/K',
            Sequence{0,0,0,0,0,0,0,0}, -- 'F',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kat',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W/(m^2*K)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg/m^3',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Sv',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Pa*s',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/m^3',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/mol',
            Sequence{0,0,0,0,0,0,0,0}, -- 'A/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'A',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C*m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'V/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'S',
            Sequence{0,0,0,0,0,0,0,0}, -- 'V',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/m^3',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/kg',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N/C',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Hz',
            Sequence{0,0,0,0,0,0,0,0}, -- 'm^3/(kg*s^2)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/K',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Lx',
            Sequence{0,0,0,0,0,0,0,0}, -- 'H',
            Sequence{0,0,0,0,0,0,0,0}, -- '1/mol',
            Sequence{0,0,0,0,0,0,0,0}, -- 'm/F',
            Sequence{2,0,-1,0,0,0,0,0}, -- 'm^2/s',
            Sequence{1,1,0,0,0,0,0,0}, -- 'm*kg',
            Sequence{1,0,0,0,1,0,0,0}, -- 'm*K',
            Sequence{1,0,-2,0,0,0,0,0}, -- 'm/s^2',
            Sequence{1,1,-1,0,0,0,0,0}, -- 'kg*m/s',
            Sequence{1,0,0,0,-1,0,0,0}, -- 'm/K',
            Sequence{1,0,-1,0,0,0,0,0}, -- 'm/s',
            Sequence{0,0,0,0,0,0,0,0}, -- 'cad/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'lm',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/T',
            Sequence{0,0,0,0,0,0,0,0}, -- 'AT/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Wb',
            Sequence{0,0,0,0,0,0,0,0}, -- 'T',
            Sequence{0,0,0,0,0,0,0,0}, -- 'AT',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg*K',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg/s',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/mol',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/(mol*K)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'H/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'F/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Pa',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Ohm',
            Sequence{0,0,0,0,0,0,0,2}, -- 'st',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/kg',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/(kg*K)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/(kg*K*Pa)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/(kg*K*m^3)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'mol*K',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W/(m*K)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'm^2/W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'K/W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'm*K/W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N/kg',
            Sequence{0,0,2,0,0,0,0,0}, -- 's^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N*m',
            Sequence{3,0,0,0,0,0,0,0}, -- 'm^3',
            Sequence{3,-1,0,0,0,0,0,0}, -- 'm^3/K',
            Sequence{3,0,-1,0,0,0,0,0}, -- 'm^3/s',
            Sequence{0,0,0,0,0,0,0,0} -- 'J/(m^3*K)'
            } in
        DerivedUnitDimensions->includes(self.dimensions) and 
        self.noOffset() and self.conversionFactor->count(1.0)=8

context Unit::defaultName():String 
  post: let BaseUnitNames : Sequence(String) = Sequence{ 'Meter', 'Kilogram', 'Second', 'Ampere', 'Kelvin', 'Mole', 'Candela', 'Radian'} in
        let DerivedUnitDimensions  : Sequence(Sequence(Real)) = Sequence{	
            Sequence{0,0,0,0,0,0,0,0}, -- 'Gy',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Gy/s',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Bq',
            Sequence{0,0,0,0,0,0,0,0}, -- 'mol/m^3',
            Sequence{0,0,0,0,0,0,0,0}, -- 'mol/kg',
            Sequence{0,0,-2,0,0,0,0,0}, -- '1/s^2',
            Sequence{-2,1,0,0,0,0,0,0}, -- 'kg*m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J*s',
            Sequence{0,0,-1,0,0,0,0,0}, -- '1/s',
            Sequence{2,0,0,0,0,0,0,0}, -- 'm^2',
            Sequence{2,0,0,0,0,0,0,0}, -- 'm^2',
            Sequence{2,0,0,0,1,0,0,0}, -- 'm^2*K',
            Sequence{2,0,0,0,-1,0,0,0}, -- 'm^2/K',
            Sequence{0,0,0,0,0,0,0,0}, -- 'F',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kat',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W/(m^2*K)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg/m^3',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Sv',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Pa*s',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/m^3',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/mol',
            Sequence{0,0,0,0,0,0,0,0}, -- 'A/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'A',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C*m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'V/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'S',
            Sequence{0,0,0,0,0,0,0,0}, -- 'V',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/m^3',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/kg',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N/C',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Hz',
            Sequence{0,0,0,0,0,0,0,0}, -- 'm^3/(kg*s^2)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/K',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Lx',
            Sequence{0,0,0,0,0,0,0,0}, -- 'H',
            Sequence{0,0,0,0,0,0,0,0}, -- '1/mol',
            Sequence{0,0,0,0,0,0,0,0}, -- 'm/F',
            Sequence{2,0,-1,0,0,0,0,0}, -- 'm^2/s',
            Sequence{1,1,0,0,0,0,0,0}, -- 'm*kg',
            Sequence{1,0,0,0,1,0,0,0}, -- 'm*K',
            Sequence{1,0,-2,0,0,0,0,0}, -- 'm/s^2',
            Sequence{1,1,-1,0,0,0,0,0}, -- 'kg*m/s',
            Sequence{1,0,0,0,-1,0,0,0}, -- 'm/K',
            Sequence{1,0,-1,0,0,0,0,0}, -- 'm/s',
            Sequence{0,0,0,0,0,0,0,0}, -- 'cad/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'lm',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/T',
            Sequence{0,0,0,0,0,0,0,0}, -- 'AT/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Wb',
            Sequence{0,0,0,0,0,0,0,0}, -- 'T',
            Sequence{0,0,0,0,0,0,0,0}, -- 'AT',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg*K',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg/s',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/mol',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/(mol*K)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'H/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'F/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Pa',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Ohm',
            Sequence{0,0,0,0,0,0,0,2}, -- 'st',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/kg',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/(kg*K)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/(kg*K*Pa)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/(kg*K*m^3)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'mol*K',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W/(m*K)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'm^2/W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'K/W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'm*K/W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N/kg',
            Sequence{0,0,2,0,0,0,0,0}, -- 's^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N*m',
            Sequence{3,0,0,0,0,0,0,0}, -- 'm^3',
            Sequence{3,-1,0,0,0,0,0,0}, -- 'm^3/K',
            Sequence{3,0,-1,0,0,0,0,0}, -- 'm^3/s',
            Sequence{0,0,0,0,0,0,0,0} -- 'J/(m^3*K)'
            } in
     let DerivedUnitNames  : Sequence(String) =Sequence{	
			'Gray',
			'GrayPerSecond',
			'Becquerel',
			'MolePerCubicMeter',
			'MolePerKilogram',
			'RadianPerSecondSquared',
			'KilogramMeterSquared',
			'JouleSecond',
			'RadianPerSecond',
			'SquareMeter',
			'SquareMeterSteradian',
			'SquareMeterKelvin',
			'SquareMeterPerKelvin',
			'Farad',
			'Katal',
			'WattPerSquareMeterKelvin',
			'KilogramPerCubicMeter',
			'Sievert',
			'PascalSecond',
			'Coulomb',
			'CoulombPerMeter',
			'CoulombPerCubicMeter',
			'CoulombPerMole',
			'AmperePerSquareMeter',
			'AmperePerRadian',
			'CoulombMeter',
			'VoltPerMeter',
			'CoulombPerSquareMeter',
			'Siemens',
			'Volt',
			'JoulePerCubicMeter',
			'Joule',
			'JoulePerSquareMeter',
			'CoulombPerKilogram',
			'Newton',
			'NewtonPerCoulomb',
			'NewtonPerMeter',
			'Hertz',
			'CubicMeterPerKilogramSecondSquared',
			'JoulePerKelvin',
			'Lux',
			'Henry',
			'PerMole',
			'MeterPerFarad',
			'SquareMeterPerSecond',
			'MeterKilogram',
			'MeterKelvin',
			'MeterPerSecondSquared',
			'KilogramMeterPerSecond',
			'MeterPerKelvin',
			'MeterPerSecond',
			'CandelaPerSquareMeter',
			'Lumen',
			'JoulePerTesla',
			'AmpereTurnPerMeter',
			'Weber',
			'Tesla',
			'AmpereTurn',
			'KilogramKelvin',
			'KilogramPerSecond',
			'KilogramPerSquareMeter',
			'KilogramPerMeter',
			'JoulePerMole',
			'JoulePerMoleKelvin',
			'HenryPerMeter',
			'FaradPerMeter',
			'Watt',
			'WattPerSteradian',
			'WattPerSquareMeterSteradian',
			'WattPerSquareMeter',
			'Pascal',
			'Ohm',
			'Steradian',
			'JoulePerKilogram',
			'JoulePerKilogramKelvin',
			'JoulePerKilogramKelvinPerPascal',
			'JoulePerKilogramKelvinPerCubicMeter',
			'MoleKelvin',
			'WattPerMeterKelvin',
			'SquareMeterKelvinPerWatt',
			'KelvinPerWatt',
			'MeterKelvinPerWatt',
			'NewtonPerKilogram',
			'SecondTimeSquared',
			'Torque',
			'CubicMeter',
			'CubicMeterPerKelvin',
			'CubicMeterPerSecond',
			'JoulePerCubicMeterKelvin'} in
   if not self.name.oclIsUndefined() or self.isDimensionless() then
     self.name
   else if self.isBaseUnit() then 
      BaseUnitNames->at(self.dimensions->indexOf(1.0))
   else if self.isDerivedUnit() then
      DerivedUnitNames->at(DerivedUnitDimensions->indexOf(self.dimensions))
   else
      Sequence{1..8}->iterate(i : Integer; acc : String = '' | if self.dimensions->at(i)<>0.0 then 
            acc.concat(BaseUnitNames->at(i)).concat('^').concat(self.dimensions->at(i)->toString()) else acc endif)
  endif endif endif

context Unit::defaultSymbol():String 
  post: let BaseUnitSymbols : Sequence(String) = Sequence{'m', 'kg', 's', 'A', 'K', 'mol', 'cd', 'rad'} in
        let DerivedUnitDimensions  : Sequence(Sequence(Real)) = Sequence{	
            Sequence{0,0,0,0,0,0,0,0}, -- 'Gy',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Gy/s',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Bq',
            Sequence{0,0,0,0,0,0,0,0}, -- 'mol/m^3',
            Sequence{0,0,0,0,0,0,0,0}, -- 'mol/kg',
            Sequence{0,0,-2,0,0,0,0,0}, -- '1/s^2',
            Sequence{-2,1,0,0,0,0,0,0}, -- 'kg*m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J*s',
            Sequence{0,0,-1,0,0,0,0,0}, -- '1/s',
            Sequence{2,0,0,0,0,0,0,0}, -- 'm^2',
            Sequence{2,0,0,0,0,0,0,0}, -- 'm^2',
            Sequence{2,0,0,0,1,0,0,0}, -- 'm^2*K',
            Sequence{2,0,0,0,-1,0,0,0}, -- 'm^2/K',
            Sequence{0,0,0,0,0,0,0,0}, -- 'F',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kat',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W/(m^2*K)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg/m^3',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Sv',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Pa*s',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/m^3',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/mol',
            Sequence{0,0,0,0,0,0,0,0}, -- 'A/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'A',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C*m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'V/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'S',
            Sequence{0,0,0,0,0,0,0,0}, -- 'V',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/m^3',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'C/kg',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N/C',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Hz',
            Sequence{0,0,0,0,0,0,0,0}, -- 'm^3/(kg*s^2)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/K',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Lx',
            Sequence{0,0,0,0,0,0,0,0}, -- 'H',
            Sequence{0,0,0,0,0,0,0,0}, -- '1/mol',
            Sequence{0,0,0,0,0,0,0,0}, -- 'm/F',
            Sequence{2,0,-1,0,0,0,0,0}, -- 'm^2/s',
            Sequence{1,1,0,0,0,0,0,0}, -- 'm*kg',
            Sequence{1,0,0,0,1,0,0,0}, -- 'm*K',
            Sequence{1,0,-2,0,0,0,0,0}, -- 'm/s^2',
            Sequence{1,1,-1,0,0,0,0,0}, -- 'kg*m/s',
            Sequence{1,0,0,0,-1,0,0,0}, -- 'm/K',
            Sequence{1,0,-1,0,0,0,0,0}, -- 'm/s',
            Sequence{0,0,0,0,0,0,0,0}, -- 'cad/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'lm',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/T',
            Sequence{0,0,0,0,0,0,0,0}, -- 'AT/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Wb',
            Sequence{0,0,0,0,0,0,0,0}, -- 'T',
            Sequence{0,0,0,0,0,0,0,0}, -- 'AT',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg*K',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg/s',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'kg/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/mol',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/(mol*K)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'H/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'F/m',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W/m^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Pa',
            Sequence{0,0,0,0,0,0,0,0}, -- 'Ohm',
            Sequence{0,0,0,0,0,0,0,2}, -- 'st',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/kg',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/(kg*K)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/(kg*K*Pa)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'J/(kg*K*m^3)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'mol*K',
            Sequence{0,0,0,0,0,0,0,0}, -- 'W/(m*K)',
            Sequence{0,0,0,0,0,0,0,0}, -- 'm^2/W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'K/W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'm*K/W',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N/kg',
            Sequence{0,0,2,0,0,0,0,0}, -- 's^2',
            Sequence{0,0,0,0,0,0,0,0}, -- 'N*m',
            Sequence{3,0,0,0,0,0,0,0}, -- 'm^3',
            Sequence{3,-1,0,0,0,0,0,0}, -- 'm^3/K',
            Sequence{3,0,-1,0,0,0,0,0}, -- 'm^3/s',
            Sequence{0,0,0,0,0,0,0,0} -- 'J/(m^3*K)'
            } in
	   let DerivedUnitSymbols  : Sequence(String) =    Sequence{	
			'Gy',
			'Gy/s',
			'Bq',
			'mol/m^3',
			'mol/kg',
			'1/s^2',
			'kg*m^2',
			'J*s',
			'1/s',
			'm^2',
			'm^2',
			'm^2*K',
			'm^2/K',
			'F',
			'kat',
			'W/(m^2*K)',
			'kg/m^3',
			'Sv',
			'Pa*s',
			'C',
			'C/m',
			'C/m^3',
			'C/mol',
			'A/m^2',
			'A',
			'C*m',
			'V/m',
			'C/m^2',
			'S',
			'V',
			'J/m^3',
			'J',
			'J/m^2',
			'C/kg',
			'N',
			'N/C',
			'N/m',
			'Hz',
			'm^3/(kg*s^2)',
			'J/K',
			'Lx',
			'H',
			'1/mol',
			'm/F',
			'm^2/s',
			'm*kg',
			'm*K',
			'm/s^2',
			'kg*m/s',
			'm/K',
			'm/s',
			'cad/m^2',
			'lm',
			'J/T',
			'AT/m',
			'Wb',
			'T',
			'AT',
			'kg*K',
			'kg/s',
			'kg/m^2',
			'kg/m',
			'J/mol',
			'J/(mol*K)',
			'H/m',
			'F/m',
			'W',
			'W',
			'W/m^2',
			'W/m^2',
			'Pa',
			'Ohm',
			'st',
			'J/kg',
			'J/(kg*K)',
			'J/(kg*K*Pa)',
			'J/(kg*K*m^3)',
			'mol*K',
			'W/(m*K)',
			'm^2/W',
			'K/W',
			'm*K/W',
			'N/kg',
			's^2',
			'N*m',
			'm^3',
			'm^3/K',
			'm^3/s',
			'J/(m^3*K)'} in
      if not self.symbol.oclIsUndefined() or self.isDimensionless() then self.symbol
      else if self.isBaseUnit() then 
         BaseUnitSymbols->at(self.dimensions->indexOf(1.0))
      else if self.isDerivedUnit() then
         DerivedUnitSymbols->at(DerivedUnitDimensions->indexOf(self.dimensions))
      else Sequence{1..8}->iterate(i : Integer; acc : String = '' | if self.dimensions->at(i)<>0.0 then 
             acc.concat(BaseUnitSymbols->at(i)).concat('^').concat(self.dimensions->at(i)->toString()) else acc endif)
      endif endif endif

-- OPERATIONS ON QUANTITIES ---------------------------

context Unit:multiplyUnits(x :Unit) :Unit  
  -- dimensions are added because they are exponents
  -- conversionFactors and offsets are not affected
  -- resulting Unit name are symbols need to be reclaculated
  post: result.dimensions = self.dimensions->sum(x.dimensions) and 
        result.conversionFactor := self.conversionFactor and
        result.offset:= self.offset and
        result.name:=aux.defaultName() and
        result.symbol:=aux.defaultSymbol()       

context Unit:divideUnits(x :Unit) :Unit  
  -- dimensions are subtracted because they are exponents
  -- conversionFactors and offsets are not affected
  -- resulting Unit name are symbols need to be reclaculated
  post: result.dimensions = self.dimensions->minus(x.dimensions) and 
        result.conversionFactor := self.conversionFactor and
        result.offset:= self.offset and
        result.name:=aux.defaultName() and
        result.symbol:=aux.defaultSymbol()       


context Unit::powerUnits(s :Real) :Unit
  -- dimensions are multiplied by scalar "s" because they are exponents
  -- conversionFactors and offsets are not affected
  -- resulting Unit name are symbols need to be reclaculated
  post: result.dimensions = self.dimensions->mult(s) and 
        result.conversionFactor := self.conversionFactor and
        result.offset:= self.offset and
        result.name:=aux.defaultName() and
        result.symbol:=aux.defaultSymbol()       

-- Auxiliary operation for Unit
context Unit::noOffset() : Boolean 
  post: result = (self.offset->count(0.0)=8)
  
-- constraints for class Unit

context u : Unit inv eightBaseDimensions:
    u.dimensions->size()=8

context u : Unit inv threeArraysSameLength:
    let n : Integer = u.dimensions->size in
        (u.offset->size = n) and (u.conversionFactor->size = n)
        
context u : Unit inv atMostOneOffset:
    u.offset->select(x | x<>0.0 )->size <= 1

-- %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
-- Definiton of class "Quantity" -- USE specific
--
-- class Quantity
--   attributes
--     value : UReal
--     unit : Unit
--   operations
--     sameUnits(x :Quantity) :Boolean 
--     compatibleUnits(u :Unit) :Boolean 
--     convertTo(u :Unit) :Quantity
--     convertToSIUnits() :Quantity
--     convertFromSIUnits(val :UReal) :Quantity
--     add(x :Quantity) : Quantity
--     minus(x :Quantity) : Quantity
--     mult(x :Quantity) : Quantity
--     divideBy(x :Quantity) : Quantity
--     abs() :Quantity
--     neg() :Quantity
--     power(s : Real) :Quantity
--     equals(x :Quantity) :Boolean 
--     distinct(x :Quantity) :Boolean
--     lessThan(x :Quantity) :Boolean
--     lessEq(x :Quantity) :Boolean
--     max(x :Quantity) :Quantity
--     min(x :Quantity) :Quantity
--     floor() :Quantity
--     round() :Quantity
--     sMult(x :Real) : Quantity
-- end
--
-- constraints

context Quantity::sameUnits(x :Quantity) :Boolean
    post: result = self.unit.equals(x.unit)

context Quantity::compatibleUnits(u :Unit) :Boolean
    post: result = self.unit.isCompatibleWith(u)
    
context Quantity::convertTo(u :Unit) :Quantity
  pre: self.compatibleUnits(x.unit)
  post: result.value = self.value.mult(self.unit.factor()/u.factor()).add((self.unit.offset->sum()-u.offset->sum())/u.factor()) 
        and result.unit = u
    
context Quantity::convertToSIUnits() :Quantity
  post: let auxUnit : Unit in
          auxUnit.dimensions = self.unit.dimensions and
          auxUnit.conversionFactor = Sequence{1,1,1,1,1,1,1,1} and
          auxUnit.offset = Sequence{0,0,0,0,0,0,0,0} and
          auxUnit.name = auxUnit.defaultName() and
          auxUnit.symbol = auxUnit.defaultSymbol() 

context Quantity::convertFromSIUnits(val :UReal) :Quantity
  post: let auxQ : Quantity in
          auxQ.unit.dimensions = self.unit.dimensions and
          auxQ.unit.conversionFactor = Sequence{1,1,1,1,1,1,1,1} and
          auxQ.unit.offset = Sequence{0,0,0,0,0,0,0,0} and
          auxQ.value = val and
          result = auxQ.convertTo(self.unit)

context Quantity::add(x :Quantity) : Quantity
  pre: self.compatibleUnits(x.unit) and x.unit.noOffset() -- operand should have no offset
  post: self.unit.noOffset() implies 
            (result.value = self.value.add(x.convertTo(self.unit).value) and result.unit =self.unit)
        and not (self.unit.noOffset()) implies
            (result = self.convertFromSIUnits(self.convertToSIUnits().value.add(x.convertToSIUnits())))

context Quantity::minus(x :Quantity) : Quantity
  pre: self.compatibleUnits(x.unit)
       and (not x.unit.noOffset() implies not self.unit.noOffset())
       -- You can subtract two units with offset (although the 
       -- result will be a "delta" unit, i.e., with no offset 
       -- But if x has offset, self should have it too.
  post: 
      (x.unit.noOffset() and self.unit.noOffset()) implies -- non of the two units have offsets
         (result.value = self.value.minus(x.convertTo(self.unit).value) and result.unit =self.unit)
  and (x.unit.noOffset() and not self.unit.noOffset()) implies -- x has no offset Unit, but "self" has
         (result = self.convertFromSIUnits(self.convertToSIUnits().value.minus(x.convertToSIUnits())))
  and (not x.unit.noOffset() and not self.unit.noOffset()) implies 
          -- neither x nor self are Delta Units, but the result should be a Delta Unit...
          -- and then we convert to the  Delta"self" unit, with no offset
         (result.value = self.value.minus(x.convertTo(self.unit).value) and 
          result.unit.dimensions = self.unit.dimensions and
          result.unit.conversionFactor = self.unit.conversionFactor and
          result.unit.offset = Sequence{0,0,0,0,0,0,0,0} and
          result.unit.name = self.unit.name.concat('delta') and
          result.unit.symbol = self.unit.symbol.concat('delta'))

context Quantity::mult(x :Quantity) : Quantity
  pre:  self.compatibleUnits(x.unit) 
        and x.unit.noOffset() and self.unit.noOffset() -- both operands should have no offset
  post: let one : Quantity = self.convertToSIUnits() in
        let other : Quantity = x.convertToSIUnits() in
         (result.value = one.value.mult(other.value) and
          result.unit = one.unit.multiplyUnits(other.unit))
 
context Quantity::divideBy(x :Quantity) : Quantity
  pre: ((x.value.x - x.value.u).max(0) > (x.value.x + x.value.u).min(0)) --not x.value.equals(0,0)
       and self.compatibleUnits(x.unit) 
       and x.unit.noOffset() and self.unit.noOffset() -- both operands should have no offset
  post: let one : Quantity = self.convertToSIUnits() in
        let other : Quantity = x.convertToSIUnits() in
         (result.value = one.value.divideBy(other.value) and
          result.unit = one.unit.divideUnits(other.unit))

context Quantity::abs() :Quantity
  post: result.value = (self.value).abs() and
        result.unit = self.unit

context Quantity::neg() :Quantity
  post: result.value = self.value.neg() and
        result.unit = self.unit

context Quantity::power(s : Real) :Quantity
  pre: s<>0 implies self.unit.noOffset() 
  post: result.value = self.value.power(s) and
        result.unit = self.unit.powerUnits(s)

context Quantity::equals(x :Quantity) :Boolean
  pre:  self.compatibleUnits(x.unit)
  post: result = self.value.equals(x.convertTo(self.unit))

context Quantity::distinct(x :Quantity) :Boolean
  pre:  self.compatibleUnits(x.unit)
  post: result = not self.equals(x)

context Quantity::lessThan(x :Quantity) :Boolean
  pre:  self.compatibleUnits(x.unit)
  post: result = self.value.lessThan(x.convertTo(self.unit)) 
        
context Quantity::lessEq(x :Quantity) :Boolean
  pre:  self.compatibleUnits(x.unit)
  post: result = self.lessThan(r) or self.equals(r)

context Quantity::max(x :Quantity) :Quantity
  pre:  self.compatibleUnits(x.unit)
  post: result = if self.lessThan(x) then x
                 else self endif

context Quantity::min(x :Quantity) :Quantity
  pre:  self.compatibleUnits(x.unit)
  post: result = if self.lessThan(x) then self
                 else x endif

context Quantity::floor() :Quantity
  post: result.value = self.value.floor() and
        result.unit = self.unit

context Quantity::round() :Quantity
  post: result.value = self.x.round() and
        result.unit = self.unit

context Quantity::sMult(x :Real) : Quantity
  post: result.value = self.value.sMult(x) and
        result.unit = self.unit


-- %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
-- Definiton of class UReal -- USE specific
-- 
-- class UReal
-- attributes
--     x : Real
--     u : Real
-- operations
--     add(r : UReal) : UReal
--     minus(r : UReal) : UReal
--     mult(r : UReal) : UReal
--     divideBy(r : UReal) : UReal
--     abs() : UReal
--     neg() : UReal
--     inverse() : UReal
--     power (s : Real) : UReal
--     sqrt() : UReal
--     lessThan(r : UReal) : Boolean 
--     lessEq(r : UReal) : Boolean 
--     equals(r : UReal) : Boolean 
--     distinct(r : UReal) : Boolean 
--     max(r : UReal) : UReal
--     min(r : UReal) : UReal
--     floor() : UReal
--     round() : UReal
--     sAdd(r : Real) : UReal
--     sMult(r : Real) : UReal
-- end

-- arithmetic operations

context UReal::add(r : UReal) : UReal
post: result.x = self.x + r.x and
      result.u = (self.u*self.u + r.u*r.u).sqrt()

context UReal::minus(r : UReal) : UReal
post: result.x = self.x - r.x and
      result.u = (self.u*self.u - r.u*r.u).sqrt()

context UReal::mult(r : UReal) : UReal
post: result.x = (self.x*r.x) and
      result.u = (r.u*r.u*self.x*self.x + self.u*self.u*r.x*r.x).sqrt()

context UReal::divideBy(r : UReal) : UReal
  pre:  (r.x - r.u).max(0) > (r.x + r.u).min(0) -- not r.equals(0,0)
  post: result.x = (self.x/r.x + (self.x*r.u*r.u)/(r.x*r.x*r.x)) and
        result.u = ((self.u*self.u/r.x) + 
                    ((r.u*r.u*self.x*self.x)/(r.x*r.x*r.x*r.x))).sqrt()

-- operations with scalars

context UReal::sAdd(r : Real) : UReal
post: result.x = self.x + r and
      result.u = self.u

context UReal::sMult(r : Real) : UReal
post: result.x = self.x*r  and
      result.u = self.u

-- unary operations

context UReal::abs() : UReal
  post: result.x = (self.x).abs() and
        result.u = self.u

context UReal::neg() : UReal
  post: result.x = -self.x and 
        result.u = self.u

context UReal::power(s : Real) : UReal
  post: result.x = self.x.power(s) + 
                   ((s*(s-1))/2)*self.x.power(s-2)*(self.u*self.u) and
        result.u = s*self.u*self.x.power(s-1)

context UReal::inverse() : UReal
    pre: (self.x - self.u).max(0) > (self.x + self.u).min(0) --not r.equals(0,0)
    post: result.x = 1/self.x and result.u = self.u

context UReal::sqrt() : UReal
    pre: (self.x + self.u)>=0.0  -- self>=0
    post: result = self.power(0.5)
    
context UReal::floor() :UReal
  post: result.x = self.x.floor() and
        result.u = self.u

context UReal::round() :UReal
  post: result.x = self.x.round() and
        result.u = self.u

-- comparison operations

context UReal::equals(r : UReal) : Boolean 
  post: result = (self.x - self.u).max(r.x - r.u) <= 
                 (self.x + self.u).min(r.x + r.u)

context UReal::distinct(r : UReal) : Boolean 
  post: result = not self.equals(r)

context UReal::compareTo(r : UReal) : Integer
  post: result = if self.equals(r) then 0
                 else if self.lessThan(r) then -1 
                       else 1 
                      endif 
                 endif

context UReal::lessThan(r : UReal) : Boolean 
  post: result = (self.x<r.x) and ((self.x + self.u)<(r.x - r.u))
 
context UReal::lessEq(r : UReal) : Boolean 
  post: result = self.lessThan(r) or self.equals(r)

context UReal::max(r :UReal) :UReal
  post: result = if r.lessThan(self) then self
                 else r endif

context UReal::min(r :UReal) :UReal
  post: result = if r.lessThan(self) then r
                 else self endif

--%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
